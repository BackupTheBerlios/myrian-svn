//
// Copyright (C) 2001, 2002 Red Hat Inc. All Rights Reserved.
//
// The contents of this file are subject to the CCM Public
// License (the "License"); you may not use this file except in
// compliance with the License. You may obtain a copy of
// the License at http://www.redhat.com/licenses/ccmpl.html
//
// Software distributed under the License is distributed on an "AS
// IS" basis, WITHOUT WARRANTY OF ANY KIND, either express or
// implied. See the License for the specific language governing
// rights and limitations under the License.
//
// $Id: //core-platform/proto/pdl/com/arsdigita/web/Application.pdl#2 $ 
// $DateTime: 2003/02/17 18:04:43 $

model com.arsdigita.web;

import com.arsdigita.kernel.PackageInstance;
import com.arsdigita.kernel.PackageType;
import com.arsdigita.kernel.Resource;
import com.arsdigita.kernel.ResourceType;
import com.arsdigita.kernel.permissions.Privilege;
import com.arsdigita.portal.Portlet;

// @author Justin Ross (justin@arsdigita.com)
// @version $Id: //core-platform/proto/pdl/com/arsdigita/web/Application.pdl#2 $

object type ApplicationType extends ResourceType {

    Privilege[0..n] relevantPrivileges =
        join application_types.application_type_id 
          to application_type_privilege_map.application_type_id,
        join application_type_privilege_map.privilege
          to acs_privileges.privilege;

}

association {
    ApplicationType[0..1] providerApplicationType =
        join application_types.provider_id 
            to application_types.application_type_id;
    ApplicationType[0..n] dependentApplicationType =
        join application_types.application_type_id
            to application_types.provider_id;
}

association {
    PackageType[0..1] packageType =
        join application_types.package_type_id 
            to apm_package_types.package_type_id;
    ApplicationType[0..n] applicationType =
        join apm_package_types.package_type_id
            to application_types.package_type_id;
}

object type Application extends Resource {
    String primaryURL = applications.primary_url VARCHAR(4000);
    // Date[1..1] timestamp = applications.timestamp TIMESTAMP;
}


association {
    PackageInstance[0..1] packageInstance =
        join applications.package_id to apm_packages.package_id;
    Application[0..1] application = 
        join apm_packages.package_id to applications.package_id;
}

query applicationForSiteNodeID {
    Application application;

    do {
        select
          acs_objects.object_id,
          acs_objects.object_type,
          acs_objects.display_name,
          acs_objects.default_domain_class,
          applications.parent_application_id,
          applications.package_id,
          applications.title,
          applications.description,
          applications.primary_url
        from
          site_nodes,
          apm_packages,
          applications,
          acs_objects
        where
          site_nodes.node_id = :siteNodeID
          -- joins
          and site_nodes.object_id = apm_packages.package_id
          and apm_packages.package_id = applications.package_id
          and applications.application_id = acs_objects.object_id
    } map {
        application.id = acs_objects.object_id;
        application.objectType = acs_objects.object_type;
        application.displayName = acs_objects.display_name;
        application.defaultDomainClass = acs_objects.default_domain_class;
        application.parentResource.id = applications.parent_application_id;
        application.packageInstance.id = applications.package_id;
        application.title = applications.title;
        application.description = applications.description;
        application.primaryURL = applications.primary_url;
    }
}
