//
// Copyright (C) 2001-2004 Red Hat, Inc.  All Rights Reserved.
//
// This program is Open Source software; you can redistribute it and/or
// modify it under the terms of the Open Software License version 2.1 as
// published by the Open Source Initiative.
//
// You should have received a copy of the Open Software License along
// with this program; if not, you may obtain a copy of the Open Software
// License version 2.1 from http://www.opensource.org/licenses/osl-2.1.php
// or by writing the Open Source Initiative c/o Lawrence Rosen, Esq.,
// 3001 King Ranch Road, Ukiah, CA 95482.
model examples;

object type Article {
    BigInteger[1..1] id = t_articles.article_id INTEGER;
    String[0..1] text   = t_articles.text CLOB;

    object key (id);
}

object type Image {
    BigInteger[1..1] id = t_images.image_id INTEGER;
    byte[][0..1] bytes    = t_images.bytes BLOB;

    object key (id);
}

association {
    Article[0..n] articles = join t_images.image_id
                               to t_article_image_map.image_id,
                             join t_article_image_map.article_id
                               to t_articles.article_id;
    Image[0..n] images = join t_articles.article_id
                           to t_article_image_map.article_id,
                         join t_article_image_map.image_id
                           to t_images.image_id;

    String[0..1] caption = t_article_image_map.caption VARCHAR(4000);
    User[0..1] user = join t_article_image_map.user_id to t_users.user_id;

    retrieve images {
        do {
            select m.caption, m.user_id, i.*
            from t_article_image_map m, t_images i
            where article_id = :articles.id
            and m.image_id = i.image_id
        } map {
            images.id = i.image_id;
            images.bytes = i.bytes;
            caption = m.caption;
            user.id = m.user_id;
        }
    }

    retrieve articles {
        do {
            select m.caption, m.user_id, a.*
            from t_article_image_map m, t_articles a
            where image_id = :images.id
            and m.article_id = a.article_id
        } map {
            user.id = m.user_id;
            articles.id = a.article_id;
            articles.text = a.text;
            caption = m.caption;
        }
    }

    add {
        do {
            insert into t_article_image_map
            (image_id, article_id, caption, user_id)
            values
            (:images.id, :articles.id, :caption, :user.id)
        }
    }

    // this must be here because of the way the association code works
    add user {}

    update {
        do {
            update t_article_image_map
            set caption = :caption
            where article_id = :articles.id
            and image_id = :images.id
        }
    }

    remove {
        do {
            delete from t_article_image_map
            where image_id = :images.id
            and article_id = :articles.id
        }
    }

}
