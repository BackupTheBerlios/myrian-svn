//
// Copyright (C) 2001, 2002, 2003 Red Hat Inc. All Rights Reserved.
//
// The contents of this file are subject to the CCM Public
// License (the "License"); you may not use this file except in
// compliance with the License. You may obtain a copy of
// the License at http://www.redhat.com/licenses/ccmpl.html
//
// Software distributed under the License is distributed on an "AS
// IS" basis, WITHOUT WARRANTY OF ANY KIND, either express or
// implied. See the License for the specific language governing
// rights and limitations under the License.
//
// $Id: //core-platform/dev/pdl/com/arsdigita/kernel/permissions.pdl#17 $
// $DateTime: 2003/08/15 13:46:34 $

model com.arsdigita.kernel.permissions;

import com.arsdigita.kernel.*;

//
// Permission
// MDSQL does not correctly handle multiple keys
//
object type Permission {
    BigDecimal[1..1] objectId = acs_permissions.object_id INTEGER;
    BigDecimal[1..1] partyId = acs_permissions.grantee_id INTEGER;
    String[1..1]     privilege = acs_permissions.privilege VARCHAR(100);
    Date[1..1]       creationDate = acs_permissions.creation_date TIMESTAMP;
    String[0..1]     creationIP = acs_permissions.creation_ip VARCHAR(50);

    // these are 0..1 to avoid persistence 1..1 checks
    // they are actually set with the id properties above
    composite ACSObject[0..1]  object =
                     join acs_permissions.object_id to acs_objects.object_id;
    composite Party[0..1]      party =
                     join acs_permissions.grantee_id to parties.party_id;
    composite Privilege[0..1]  priv =
                     join acs_permissions.privilege
                       to acs_privileges.privilege;
    User[0..1]       creationUser =
                     join acs_permissions.creation_user to users.user_id;

    object key (objectId, partyId, privilege);

    retrieve {
        do {
            select acs_permissions.object_id,
                   acs_permissions.grantee_id,
                   acs_permissions.privilege,
                   acs_permissions.creation_date,
                   acs_permissions.creation_ip
              from acs_permissions
             where acs_permissions.object_id = :objectId
               and acs_permissions.grantee_id = :partyId
               and acs_permissions.privilege = :privilege
        } map {
            objectId = acs_permissions.object_id;
            partyId = acs_permissions.grantee_id;
            privilege = acs_permissions.privilege;
            creationDate = acs_permissions.creation_date;
            creationIP = acs_permissions.creation_ip;
        }
    }

    retrieve all {
        do {
            select acs_permissions.object_id,
                   acs_permissions.grantee_id,
                   acs_permissions.privilege,
                   acs_permissions.creation_date,
                   acs_permissions.creation_ip
            from acs_permissions
        } map {
            objectId = acs_permissions.object_id;
            partyId = acs_permissions.grantee_id;
            privilege = acs_permissions.privilege;
            creationDate = acs_permissions.creation_date;
            creationIP = acs_permissions.creation_ip;
        }
    }

    insert {
        do {
            insert into acs_permissions
            (object_id, grantee_id, privilege,
             creation_user, creation_date, creation_ip)
            values
            (:objectId, :partyId, :privilege,
             :creationUser.id, :creationDate, :creationIP)
        }
    }


    update {}


    delete {
        do {
            delete from acs_permissions
             where object_id = :objectId
               and grantee_id = :partyId
               and privilege = :privilege
        }
    }

  retrieve creationUser {
        do {
            select users.user_id,
	               users.screen_name,
	               parties.uri, acs_objects.object_type
              from users, parties, acs_objects, acs_permissions
             where parties.party_id = users.user_id
	           and acs_objects.object_id = parties.party_id
                   and acs_permissions.creation_user = users.user_id
                   and acs_permissions.object_id = :objectId
                   and acs_permissions.grantee_id = :partyId
                   and acs_permissions.privilege = :privilege
        } map {
            creationUser.id = users.user_id;
            creationUser.screenName = users.screen_name;
            creationUser.uri = parties.uri;
            creationUser.objectType = acs_objects.object_type;
        }
  }

  add creationUser {}
  remove creationUser {}

}


// query that is encapsulated by ObjectDirectPermissionCollection.
// This is one nasty query that includes display information about
// the grantee, which may be a user or group (hence the union).
query ObjectDirectPermissionCollection {
    String privilege;
    Boolean isInherited;
    BigDecimal granteeID;
    Boolean granteeIsUser;
    String granteeName;
    String granteeEmail;
    String granteeGivenName;
    String granteeFamilyName;
    BigDecimal granteeNameID;
    do {
        select acs_permissions.privilege,
               0 as inherited_p,
               acs_permissions.grantee_id,
               parties.user_p,
               parties.name,
               parties.primary_email,
               parties.given_name,
               parties.family_name,
               parties.name_id
        from acs_permissions,
             (select groups.group_id as party_id,
                     0 as user_p,
                     groups.name as name,
                     parties.primary_email as primary_email,
                     '' as given_name,
                     '' as family_name,
                     0 as name_id
              from groups, parties
              where groups.group_id = parties.party_id) parties
        where acs_permissions.object_id = :objectID
          and acs_permissions.grantee_id = parties.party_id
        UNION ALL
        select acs_permissions.privilege,
               0 as inherited_p,
               acs_permissions.grantee_id,
               parties.user_p,
               parties.name,
               parties.primary_email,
               parties.given_name,
               parties.family_name,
               parties.name_id
        from acs_permissions,
             (select users.user_id as party_id,
                     1 as user_p,
                     '' as name,
                     parties.primary_email as primary_email,
                     person_names.given_name as given_name,
                     person_names.family_name as family_name,
                     users.name_id
              from users, parties, person_names
              where users.user_id = parties.party_id
                and users.name_id = person_names.name_id) parties
        where acs_permissions.object_id = :objectID
          and acs_permissions.grantee_id = parties.party_id
    } map {
        privilege = acs_permissions.privilege;
        isInherited = inherited_p;
        granteeID = acs_permissions.grantee_id;
        granteeIsUser = parties.user_p;
        granteeName = parties.name;
        granteeEmail = parties.primary_email;
        granteeGivenName = parties.given_name;
        granteeFamilyName = parties.family_name;
        granteeNameID = parties.name_id;
    }
}



query PermissionFilterQuery {
    BigDecimal objectID;
    do {
        select oc.object_id
        from object_context oc
        where (exists
                   (select 1
                      from granted_context_non_leaf_map c,
                           acs_permissions p
                     where c.implied_context_id = p.object_id
		       and context_id = c.object_id
                       and (p.grantee_id in :partyList or
                            exists (select 1
                                    from group_member_trans_map
                                    where member_id in :partyList
				    and p.grantee_id = group_id))
                        and p.privilege in :privilegeList
                   )
               or exists
                   (select 1
                      from acs_permissions p
		      where p.object_id = oc.object_id
                      and (p.grantee_id in :partyList or
                            exists (select 1
                                    from group_member_trans_map
                                    where member_id in :partyList
				    and p.grantee_id = group_id))
                        and p.privilege in :privilegeList
                   )
              )
    } map {
        objectID=oc.object_id;
    }
}

query GroupPermissionFilterQuery {
    BigDecimal objectID;
    do {
        select oc.object_id
        from object_context oc
        where (exists
                   (select 1
                      from granted_context_non_leaf_map c,
                           acs_permissions p
                     where c.implied_context_id = p.object_id
		       and context_id = c.object_id
                       and exists (select 1
                                   from group_subgroup_trans_map
                                   where subgroup_id in :partyList
				   and p.grantee_id = group_id)
                        and p.privilege in :privilegeList
                   )
               or exists
                   (select 1
                      from acs_permissions p
		      where oc.object_id = p.object_id
                      and exists (select 1
                                   from group_subgroup_trans_map
                                   where subgroup_id in :partyList
				   and p.grantee_id = group_id)
                        and p.privilege in :privilegeList
                   )
              )
    } map {
        objectID=oc.object_id;
    }
}

// Three cases for grantee:
//  - User
//  - Party
//  - Site-wide admin

query ObjectPermissionFilterQuery {
    BigDecimal granteeID;
    do {
        select member_id as grantee_id
          from party_member_trans_map m,
               (select p.grantee_id
                  from granted_context_non_leaf_map c,
                       acs_permissions p
                 where c.object_id = p.object_id
                   and c.object_id = :objectID
                   and p.privilege in :privilegeList
                 union all
                select p.grantee_id
                  from acs_permissions p
                 where p.object_id = :objectID
                   and p.privilege in :privilegeList) g
         where m.party_id = g.grantee_id
         union all
        select grantee_id
          from acs_permissions
         where object_id = 0
           and privilege in :privilegeList
    } map {
        granteeID = p.grantee_id;
    }
}

query CheckPermissionForUser {
    Boolean hasPermission;
    do {
        select 1 as permission_p
        from object_context
        where object_id = :objectID
          and (exists (
                   select 1
                     from granted_context_non_leaf_map c,
                          acs_permissions p
                    where c.implied_context_id = p.object_id
		      and context_id = c.object_id
                      and (  p.grantee_id in :partyList or
                             exists
                                (select 1
                                   from group_member_trans_map
                                  where member_id in :partyList
				  and p.grantee_id = group_id)
                          )
                      and p.privilege in :privilegeList
                  )
               or exists (
                    select 1
                      from acs_permissions p
		      where :objectID = p.object_id
                      and (  p.grantee_id in :partyList or
                              exists
                                  (select 1
                                     from group_member_trans_map
                                    where member_id in :partyList
				    and p.grantee_id = group_id)
                           )
                       and p.privilege in :privilegeList
                  )
              )
    } map {
        hasPermission = permission_p;
    }
}

query CheckPermissionForGroup {
    Boolean hasPermission;
    do {
        select 1 as permission_p
        from object_context
        where object_id = :objectID
          and (exists (
                   select 1
                     from granted_context_non_leaf_map c,
                          acs_permissions p
                    where c.implied_context_id = p.object_id
		      and context_id = c.object_id
                      and exists
                                (select 1
                                   from group_subgroup_trans_map
                                  where subgroup_id in :partyList
				   and p.grantee_id = group_id)
                      and p.privilege in :privilegeList
                  )
               or exists (
                    select 1
                      from acs_permissions p
                     where p.object_id = :objectID
		     and exists
                                  (select 1
                                     from group_subgroup_trans_map
                                    where subgroup_id in :partyList
				     and p.grantee_id = group_id)
                       and p.privilege in :privilegeList
                  )
              )
    } map {
        hasPermission = permission_p;
    }
}

query CheckUninheritedPermissionForUser {
    Boolean hasPermission;
    do {
        select 1 as permission_p from dual
        where exists (
            select 1
            from  acs_permissions
            where acs_permissions.object_id = :objectID
              and (acs_permissions.grantee_id in :partyList
                   or exists  (
                          select 1
                          from group_member_trans_map
                          where member_id in :partyList
			  and acs_permissions.grantee_id = group_id
                   ))
              and (acs_permissions.privilege in :privilegeList)
        )
    } map {
        hasPermission = permission_p;
    }
}

query CheckUninheritedPermissionForGroup {
    Boolean hasPermission;
    do {
        select 1 as permission_p from dual
        where exists (
            select 1
            from  acs_permissions
            where acs_permissions.object_id = :objectID
              and (exists (
                          select 1
                          from group_subgroup_trans_map
                          where subgroup_id in :partyList
			  and acs_permissions.grantee_id = group_id
                   ))
              and (acs_permissions.privilege in :privilegeList)
        )
    } map {
        hasPermission = permission_p;
    }
}

query CheckDirectGrant {
    Boolean hasPermission;
    do {
        select 1 as permission_p from dual
        where exists (
            select 1
            from acs_permissions
            where acs_permissions.grantee_id = :partyID
              and acs_permissions.object_id = :objectID
              and acs_permissions.privilege in :privileges
        )
    } map {
        hasPermission = permission_p;
    }
}

data operation RevokePartyPermissions {
    do {
        delete from acs_permissions
        where grantee_id = :partyID
    }
}

query PrivilegesForUser {
    String privilege;
    do {
          select acs_permissions.privilege
            from acs_permissions
           where (acs_permissions.grantee_id = :partyID
                  or exists (
                          select 1
                          from group_member_trans_map
                          where member_id = :partyID
			  and acs_permissions.grantee_id = group_id
                  ))
             and acs_permissions.object_id = :objectID
          UNION
          select acs_permissions.privilege
            from acs_permissions,
                 object_context oc,
                 granted_trans_context_index map
           where oc.object_id = :objectID
             and oc.context_id = map.object_id
             and map.implied_context_id = acs_permissions.object_id
             and (acs_permissions.grantee_id = :partyID
                   or exists (
                          select 1
                          from group_member_trans_map
                          where member_id = :partyID
			  and acs_permissions.grantee_id = group_id
                  ))
    } map {
        privilege = acs_permissions.privilege;
    }
}

query PrivilegesForGroup {
    String privilege;
    do {
          select acs_permissions.privilege
            from acs_permissions
           where exists (
                          select 1
                          from group_subgroup_trans_map
                          where subgroup_id = :partyID
			  and acs_permissions.grantee_id = group_id
                 )
             and acs_permissions.object_id = :objectID
          UNION
          select acs_permissions.privilege
            from acs_permissions,
                 object_context oc,
                 granted_trans_context_index map
           where oc.object_id = :objectID
             and oc.context_id = map.object_id
             and map.implied_context_id = acs_permissions.object_id
             and exists (
                          select 1
                          from group_subgroup_trans_map
                          where subgroup_id = :partyID
			  and acs_permissions.grantee_id = group_id
                 )
    } map {
        privilege = acs_permissions.privilege;
    }
}




//
// Privilege
//
object type Privilege {
    String privilege = acs_privileges.privilege VARCHAR(100);

    object key (privilege);
}


//
// ObjectContext
//
object type ObjectContext {
    BigDecimal[1..1] objectId = object_context.object_id INTEGER;

    composite ACSObject[1..1] object = join object_context.object_id
                                         to acs_objects.object_id;
    composite ACSObject[0..1] context = join object_context.context_id
                                          to acs_objects.object_id;
    object key (objectId);

    insert {
        // Because the insertion occurs via trigger,
        // acs_objects_context_in_tr.

        do {
            update object_context
               set context_id = :context.id
             where object_id = :objectId
        }
    }
}


object type ParameterizedPrivilege {
    String[1..1] basePrivilege = parameterized_privileges.base_privilege VARCHAR(100);
    String[1..1] paramKey = parameterized_privileges.param_key VARCHAR(100);
    String[0..1] paramName = parameterized_privileges.param_name VARCHAR(100);

    // this are 0..1 to avoid persistence 1..1 checks
    // actually set with the basePrivilege property above
    Privilege[0..1] basePriv =
        join parameterized_privileges.base_privilege
          to acs_privileges.privilege;

    object key (paramKey, basePrivilege);

    insert {
        do {
            insert into parameterized_privileges
            (base_privilege, param_key, param_name)
            values
            (:basePrivilege, :paramKey, :paramName)
        }
    }

    update {
        do {
            update parameterized_privileges
            set base_privilege = :basePrivilege,
            param_key = :paramKey,
            param_name = :paramName
        }
    }

    delete {
        do {
            delete from parameterized_privileges
            where param_key = :paramKey
            and base_privilege = :basePrivilege
        }
    }

    retrieve {
        do {
            select base_privilege, param_key, param_name
            from parameterized_privileges
            where base_privilege = :basePrivilege
              and param_key = :paramKey
        } map {
            basePrivilege = parameterized_privileges.base_privilege;
            paramKey = parameterized_privileges.param_key;
            paramName = parameterized_privileges.param_name;
        }
    }

    retrieve all {
        do {
            select base_privilege, param_key, param_name
            from parameterized_privileges
        } map {
            basePrivilege = parameterized_privileges.base_privilege;
            paramKey = parameterized_privileges.param_key;
            paramName = parameterized_privileges.param_name;
        }
    }
}

// query that is encapsulated by ObjectPermissionCollection.
// This is one nasty query that includes display information about
// the grantee, which may be a user or group (hence the union).
query ObjectPermissionCollection {
    String privilege;
    Boolean isInherited;
    BigDecimal granteeID;
    Boolean granteeIsUser;
    String granteeName;
    String granteeEmail;
    PersonName granteePersonName;
    do {
        select acs_permissions.privilege,
               CASE
                 WHEN acs_permissions.object_id = :objectID THEN
                    0
                 ELSE
                    1
               END as inherited_p,
               acs_permissions.grantee_id,
               parties.user_p,
               parties.name,
               parties.primary_email,
               parties.given_name,
               parties.family_name,
               parties.name_id
        from
             (select
                    p.privilege,
                    p.object_id,
                    p.grantee_id
             from
                  acs_permissions p,
                  granted_trans_context_map map
             where map.object_id = :objectID
               and p.object_id = map.implied_context_id) acs_permissions,
             (select groups.group_id as party_id,
                     0 as user_p,
                     groups.name as name,
                     parties.primary_email as primary_email,
                     '' as given_name,
                     '' as family_name,
                     0 as name_id
              from groups, parties
              where groups.group_id = parties.party_id) parties
        where acs_permissions.grantee_id = parties.party_id
        UNION ALL
        select acs_permissions.privilege,
               CASE
                 WHEN acs_permissions.object_id = :objectID THEN
                    0
                 ELSE
                    1
               END as inherited_p,
               acs_permissions.grantee_id,
               parties.user_p,
               parties.name,
               parties.primary_email,
               parties.given_name,
               parties.family_name,
               parties.name_id
        from 
             (select
                    p.privilege,
                    p.object_id,
                    p.grantee_id
             from
                  acs_permissions p,
                  granted_trans_context_map map
             where map.object_id = :objectID
               and p.object_id = map.implied_context_id) acs_permissions,
             (select users.user_id as party_id,
                     1 as user_p,
                     '' as name,
                     parties.primary_email as primary_email,
                     person_names.given_name as given_name,
                     person_names.family_name as family_name,
                     users.name_id
              from users, parties, person_names
              where users.user_id = parties.party_id
                and users.name_id = person_names.name_id) parties
        where acs_permissions.grantee_id = parties.party_id
    } map {
        privilege = acs_permissions.privilege;
        isInherited = inherited_p;
        granteeID = acs_permissions.grantee_id;
        granteeIsUser = parties.user_p;
        granteeName = parties.name;
        granteeEmail = parties.primary_email;
        granteePersonName.id = parties.name_id;
        granteePersonName.givenName = parties.given_name;
        granteePersonName.familyName = parties.family_name;
    }
}
