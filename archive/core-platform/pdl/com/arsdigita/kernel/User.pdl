//
// Copyright (C) 2001, 2002 Red Hat Inc. All Rights Reserved.
//
// The contents of this file are subject to the CCM Public
// License (the "License"); you may not use this file except in
// compliance with the License. You may obtain a copy of
// the License at http://www.redhat.com/licenses/ccmpl.html
//
// Software distributed under the License is distributed on an "AS
// IS" basis, WITHOUT WARRANTY OF ANY KIND, either express or
// implied. See the License for the specific language governing
// rights and limitations under the License.
//
// $Id: //core-platform/dev/pdl/com/arsdigita/kernel/User.pdl#10 $
// $DateTime: 2003/05/12 18:19:45 $

model com.arsdigita.kernel;

object type User extends Party {
    PersonName[1..1] name = join users.name_id to person_names.name_id;
    unique String screenName = users.screen_name VARCHAR(100);

    reference key (users.user_id);

    // name_id is set during insert, and should never change.
    add name{}
}

data operation ClearUserFromGroups {
    do {
        delete from group_member_map
        where member_id = :memberID
    }
}

query UserPrimaryEmail {
    BigDecimal userID;
    String primaryEmailAddress;
    String lowerScreenName;
    String screenName;
    String lowerPrimaryEmailAddress;
    do {
	select users.user_id,
	       users.screen_name,
	       lower(users.screen_name) as lower_screen_name,
	       lower(parties.primary_email) as lower_primary_email,
	       parties.primary_email
	  from users, 
	       parties
	 where users.user_id = parties.party_id
    } map {
	  userID = users.user_id;
	  screenName = users.screen_name;
	  lowerScreenName = lower_screen_name;
	  primaryEmailAddress = parties.primary_email;
	  lowerPrimaryEmailAddress = lower_primary_email;
    }
}

query RetrieveUsers {
    // This should be changed to just return a User object.
    BigDecimal userID;
    String screenName;
    String firstName;
    String lastName;
    String searchName;
    String primaryEmail;
    String objectType;
    do {
        select users.user_id,
               users.screen_name,
               person_names.given_name,
               person_names.family_name,
               parties.primary_email,
               lower(nvl(users.screen_name,'') || ' ' || person_names.given_name || ' ' || person_names.family_name) as search_name,
               o.object_type
          from users, person_names, parties, acs_objects o
         where users.name_id = person_names.name_id
           and o.object_id = users.user_id
           and users.user_id = parties.party_id and
               not exists (
                   select 1 from group_member_map g
                   where g.member_id = users.user_id
                     and g.group_id  = :excludeGroupId)
    } map {
        userID = user_id;
        screenName = screen_name;
        firstName = given_name;
        lastName = family_name;
        searchName = search_name;
        primaryEmail = primary_email;
        objectType = o.object_type;
    }
}

query RetrieveAllUsers {
    // This should be changed to just return a User object.
    BigDecimal userID;
    String screenName;
    String firstName;
    String lastName;
    String searchName;
    String primaryEmail;
    String objectType;
    do {
        select users.user_id,
               users.screen_name,
               person_names.given_name,
               person_names.family_name,
               parties.primary_email,
               lower(nvl(users.screen_name,'') || ' ' || person_names.given_name || ' ' || person_names.family_name) as search_name,
               o.object_type
          from users, person_names, parties, acs_objects o
         where users.name_id = person_names.name_id
           and o.object_id = users.user_id
           and users.user_id = parties.party_id
    } map {
        userID = user_id;
        screenName = screen_name;
        firstName = given_name;
        lastName = family_name;
        searchName = search_name;
        primaryEmail = primary_email;
        objectType = o.object_type;
    }
}
