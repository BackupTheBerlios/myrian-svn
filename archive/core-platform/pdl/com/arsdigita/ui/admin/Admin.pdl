//
// Copyright (C) 2001, 2002 Red Hat Inc. All Rights Reserved.
//
// The contents of this file are subject to the CCM Public
// License (the "License"); you may not use this file except in
// compliance with the License. You may obtain a copy of
// the License at http://www.redhat.com/licenses/ccmpl.html
//
// Software distributed under the License is distributed on an "AS
// IS" basis, WITHOUT WARRANTY OF ANY KIND, either express or
// implied. See the License for the specific language governing
// rights and limitations under the License.
//
// $Id: //core-platform/dev/pdl/com/arsdigita/ui/admin/Admin.pdl#8 $ 
// $DateTime: 2003/05/20 16:55:13 $

model com.arsdigita.ui.admin;

query AllNoParentGroups {
    BigDecimal groupID;
    do {
        select group_id
          from groups
         where not exists (
	                   select 1
                           from group_subgroup_map
			   where group_id = subgroup_id)
    } map {
        groupID = group_id;
    }
}

// Retrieve subgroup count for giving group id
// without first retrieve information about group.
query SubGroupCount {
   BigDecimal count;

   do {
      select count(subgroup_id) as count 
      from group_subgroup_map
      where group_id = :groupID
   } map {
      count = count;
   }
}
// This is used to retrieve all registers in one SQL query instead
// of three separate queries, one to retrieve person name, one to 
// retrieve user email, and one to retrieve user information.
query RetrieveAllUsersInfo {
    BigDecimal userID;
    String userDisplayName;
    String userScreenName;
    String userPrimaryEmail;
  do {
    select acs_objects.object_id, 
           acs_objects.display_name, 
           users.screen_name,
           parties.primary_email
    from acs_objects,
         parties,
         users
    where acs_objects.object_id = users.user_id and
          parties.party_id = users.user_id 
  } map {
    userID = acs_objects.object_id;
    userDisplayName = acs_objects.display_name;
    userScreenName = users.screen_name;
    userPrimaryEmail = parties.primary_email;
  }
}

