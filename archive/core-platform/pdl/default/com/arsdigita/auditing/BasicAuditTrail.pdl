//
// Copyright (C) 2001 ArsDigita Corporation. All Rights Reserved.
//
// The contents of this file are subject to the ArsDigita Public 
// License (the "License"); you may not use this file except in
// compliance with the License. You may obtain a copy of
// the License at http://www.arsdigita.com/ADPL.txt
//
// Software distributed under the License is distributed on an "AS
// IS" basis, WITHOUT WARRANTY OF ANY KIND, either express or
// implied. See the License for the specific language governing
// rights and limitations under the License.
//

model com.arsdigita.auditing;

//Note, this doesn't need to extend ACSObject since
//we its doesn't require any services such as
//permissioning, etc

import com.arsdigita.kernel.*;

object type BasicAuditTrail {
  BigDecimal[1..1]  id;
  User[0..1]        creationUser;
  Date[1..1]        creationDate;
  String[0..1]      creationIP;
  User[0..1]        lastModifiedUser;
  Date[0..1]        lastModifiedDate;
  String[0..1]      lastModifiedIP;


  object key (id);

  retrieve {
    do {
      select creation_date, creation_ip, last_modified, modifying_ip
        from
      acs_auditing
        where
      object_id = :id
    } map {
        creationDate = acs_auditing.creation_date;
        creationIP = acs_auditing.creation_ip;
        lastModifiedDate = acs_auditing.last_modified;
        lastModifiedIP = acs_auditing.modifying_ip;
    }
  }

  insert {
    do {
    insert into acs_auditing (
      object_id, creation_user, creation_date, creation_ip,
      last_modified, modifying_user, modifying_ip
    ) values (
      :id, :creationUser.id, :creationDate, :creationIP,
      :lastModifiedDate, :lastModifiedUser.id, :lastModifiedIP
    )
    }
  }

  update {
    do {
    update acs_auditing
    set last_modified = :lastModifiedDate,
        modifying_user = :lastModifiedUser.id,
        modifying_ip = :lastModifiedIP
    where object_id = :id
    }
  }

  delete {
    do {
    delete from acs_auditing
      where object_id = :id
    }
  }

  retrieve creationUser {
        do {
            select users.user_id, 
	               users.screen_name,
	               parties.uri, acs_objects.object_type
              from users, parties, acs_objects, acs_auditing
             where parties.party_id = users.user_id
	           and acs_objects.object_id = parties.party_id
                   and acs_auditing.creation_user = users.user_id
                   and acs_auditing.object_id = :id
        } map {
            creationUser.id = users.user_id;
            creationUser.screenName = users.screen_name;
            creationUser.uri = parties.uri;
            creationUser.objectType = acs_objects.object_type;
        }
  }

  add creationUser {}
  remove creationUser {}

  retrieve lastModifiedUser {
        do {
            select users.user_id, 
	               users.screen_name,
	               parties.uri, acs_objects.object_type
              from users, parties, acs_objects, acs_auditing
             where parties.party_id = users.user_id
	           and acs_objects.object_id = parties.party_id
                   and acs_auditing.modifying_user = users.user_id
                   and acs_auditing.object_id = :id
        } map {
            lastModifiedUser.id = users.user_id;
            lastModifiedUser.screenName = users.screen_name;
            lastModifiedUser.uri = parties.uri;
            lastModifiedUser.objectType = acs_objects.object_type;
        }
  }
  add lastModifiedUser {}
  remove lastModifiedUser {}
}

//Used to retrieve the BasicAuditTrail for an ACSObject
query auditTrailForACSObject {
	do {
            select object_id from acs_auditing
	} map {
            id = acs_auditing.object_id;
        }
}