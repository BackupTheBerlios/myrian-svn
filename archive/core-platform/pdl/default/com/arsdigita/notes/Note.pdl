//
// Copyright (C) 2001 ArsDigita Corporation. All Rights Reserved.
//
// The contents of this file are subject to the ArsDigita Public 
// License (the "License"); you may not use this file except in
// compliance with the License. You may obtain a copy of
// the License at http://www.arsdigita.com/ADPL.txt
//
// Software distributed under the License is distributed on an "AS
// IS" basis, WITHOUT WARRANTY OF ANY KIND, either express or
// implied. See the License for the specific language governing
// rights and limitations under the License.
//

//
// Represent notes.
//
// @author <a href="mailto:scott@arsdigita.com">Scott Seago</a>
// @author <a href="mailto:tzumainn@arsdigita.com">Tzu-Mainn Chen</a>
// @version $Id: //core-platform/dev/pdl/default/com/arsdigita/notes/Note.pdl#3 $ by $Author: rhs $, $DateTime: 2002/08/09 15:10:37 $
//

model com.arsdigita.notes;

import com.arsdigita.kernel.*;

object type Note extends ACSObject {
    String[1..1] title = notes.title VARCHAR(255);
    String body = notes.body VARCHAR(4000);
    NoteTheme[0..1] theme = join notes.theme_id to note_themes.theme_id;

    reference key (notes.note_id);
}

query NotesRetrieveAll {
    BigDecimal id;
    String title;
    String body;
    BigDecimal categoryID;
    String categoryName;
    Date creationDate;
    Date lastModified;
    BigDecimal creationUser;
    BigDecimal modifiedBy;
    BigDecimal noteNum;

    do {
        select n.note_id,
               m.category_id,
               m.name,
               n.title,
               n.body, 
               n.creation_date,
               n.last_modified, 
               n.modifying_user,
               n.creation_user,
               n.note_rownum
        from (select nt.*,
                     a.*,
                     rownum as note_rownum
              from notes nt,
                   acs_auditing a
              where nt.note_id = a.object_id (+)) n,
             (select m.*,
                     name
              from cat_object_category_map m,
                   cat_categories c
              where c.category_id=m.category_id) m
        where n.note_id = m.object_id(+)
    } map {
        id = n.note_id;
        title = n.title;
        body = n.body;
        categoryID = m.category_id;
        categoryName = m.name;
        creationDate = n.creation_date;
        lastModified = n.last_modified;
        creationUser = n.creation_user;
        modifiedBy = n.modifying_user;
        noteNum = n.note_rownum;
    }
}

object type NoteTheme extends ACSObject {
    unique String[1..1] name = note_themes.name VARCHAR(200);
    Stylesheet[0..n] style = join note_themes.theme_id
                               to theme_stylesheet_map.theme_id,
                             join theme_stylesheet_map.stylesheet_id
                               to acs_stylesheets.stylesheet_id;
    reference key (note_themes.theme_id);
}
